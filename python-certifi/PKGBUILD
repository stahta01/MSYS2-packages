# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=certifi
pkgname="python-${_realname}"
pkgver=2026.2.25
pkgrel=1
pkgdesc="Python package for providing Mozilla's CA Bundle"
url='https://pypi.python.org/pypi/certifi'
msys2_repository_url='https://github.com/certifi/python-certifi'
msys2_references=(
  'archlinux: python-certifi'
  "purl: pkg:pypi/certifi"
)
license=('spdx:MPL-2.0')
arch=('any')
depends=("python")
makedepends=(
  "python-setuptools"
  "python-build"
  "python-installer"
)
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e887ab5cee78ea814d3472169153c2d12cd43b14bd03329a39a9c6e2e80bfba7')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
