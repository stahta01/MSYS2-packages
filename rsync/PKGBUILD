# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rsync
pkgver=3.2.2
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="https://rsync.samba.org"
groups=('net-utils')
license=('GPL3')
makedepends=('openssl-devel' 'libzstd-devel' 'liblz4-devel')
depends=('perl')
source=("https://download.samba.org/pub/rsync/src/$pkgname-$pkgver.tar.gz"
        rsync-3.2.0-msys2.patch)
sha256sums=('644bd3841779507665211fd7db8359c8a10670c57e305b4aab61b4e40037afa8'
            '16a45e49035f09da7ff342f979aa6986f5039014fc8e847763d0dd251cd2642b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/rsync-3.2.0-msys2.patch
  #patch -p1 -i ${srcdir}/ssh-6-option.patch

  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
      --build=${CHOST} \
      --prefix=/usr \
      --disable-xxhash
  
  make reconfigure
  make man all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"
}
